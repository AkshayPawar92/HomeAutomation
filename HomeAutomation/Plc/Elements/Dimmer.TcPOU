<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.13">
  <POU Name="Dimmer" Id="{72de836e-7852-4ce8-a2be-acbaaea23600}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK Dimmer
VAR_INPUT
	DimUp_ON: BOOL;
	DimDown_OFF: BOOL;
END_VAR
VAR_OUTPUT
	output: INT;
END_VAR
VAR
	tonLongPress_01: TON;
	tonLongPress_02: TON;
	step: INT;
	dimmer_perc: REAL;

	rampUp: RampUp;
	rampDown: RampDown;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[	
CASE step OF
	0:	//	Key pressed
		IF DimUp_ON THEN
			step := 10;
		ELSIF DimDown_OFF THEN
			step := 50;	
		END_IF
		rampUp(IN := FALSE, output:= dimmer_perc);
		rampDown(In := FALSE, output:= dimmer_perc);
		tonLongPress_01(IN := FALSE);
		tonLongPress_02(IN := FALSE);
		
		
	10:	//	Short Press - On
		tonLongPress_01(IN:= DimUp_ON, PT:= T_LONG_PRESS, Q=> , ET=> );
		IF tonLongPress_01.Q THEN
			step :=	20;
		ELSIF (NOT DimUp_ON) THEN
			dimmer_perc	:=	32767;
			step		:=	0;
		END_IF
	
	20: // Long Press - Up
		rampUp(
			IN:= TRUE, 
			RampUpTime:= RAMP_UP_TIME, 
			Ramp_lowerLimit:= 0, 
			Ramp_upperLimit:= 32767, 
			output:= dimmer_perc, 
			InProgress=> );
			
		IF (NOT DimUp_ON) THEN
			step	:=	0;
		END_IF
	

	50:	//	Short press - Off
		tonLongPress_02(IN:= DimDown_OFF, PT:= T_LONG_PRESS, Q=> , ET=> );
		IF tonLongPress_02.Q THEN
			step :=	60;
		ELSIF (NOT DimDown_OFF) THEN
			dimmer_perc	:=	0;
			step		:=	0;
		END_IF
	
	60:	//	Long press - Down
		rampDown(
			IN:= TRUE, 
			RampDownTime:= RAMP_DOWN_TIME, 
			Ramp_lowerLimit:= 0, 
			Ramp_upperLimit:= 32767, 
			output:= dimmer_perc, 
			InProgress=> );
		
		IF (NOT DimDown_OFF) THEN
			step	:=	0;
		END_IF
	
END_CASE
	
	output	:=	REAL_TO_INT(dimmer_perc);
 
				
]]></ST>
    </Implementation>
    <LineIds Name="Dimmer">
      <LineId Id="255" Count="63" />
      <LineId Id="9" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>